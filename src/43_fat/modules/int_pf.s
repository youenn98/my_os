;************************************************************************
;	
;************************************************************************
int_pf:
		
		push	ebp								
		mov		ebp, esp					
											
		pusha									; 
		push	ds								; 
		push	es								; 

		;---------------------------------------
		; 段选择器设定
		;---------------------------------------
		mov		ax, 0x0010						; 
		mov		ds, ax							; 
		mov		es, ax							; 

		;---------------------------------------
		; 确认异常生成的地址
		;---------------------------------------
		mov		eax, cr2						; // CR2
		and		eax, ~0x0FFF					; // 4K byte以内的访问
		cmp		eax, 0x0010_7000				; ptr = 访问地址;
		jne		.10F							; if (0x0010_7000 == ptr)
												; {
		;---------------------------------------
		; page的有效化
		;---------------------------------------
		mov		[0x00106000 + 0x107 * 4], dword 0x00107007	; // page的有效化

		;---------------------------------------
		; 画图参数
		;---------------------------------------
		cdecl	memcpy, 0x0010_7000, DRAW_PARAM, rose_size	; 画图参数task_3用
												; }
		jmp		.10E							; else
.10F:											; {
		;---------------------------------------
		; stack的调整
		;---------------------------------------
		add		esp, 4							; pop es
		add		esp, 4							; pop ds
		popa									; 
		pop		ebp								; 

		;---------------------------------------
		; task结束的处理
		;---------------------------------------
		pushf									; // EFLAGS
		push	cs								; // CS
		push	int_stop						; // stack表示处理

		mov		eax, .s0						; // 中断类别
		iret
.10E:											; }
	
		pop		es								; 
		pop		ds								; 
		popa									; 

		mov		esp, ebp
		pop		ebp

		;---------------------------------------
		; 将error_code pop掉
		;---------------------------------------
		add		esp, 4							; // 将error_code pop掉

		iret

.s0		db	" < PAGE FAULT > ", 0

